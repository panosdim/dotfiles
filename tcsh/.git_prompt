#!/usr/bin/env csh 

# get current branch in git repo
alias __git_current_branch 'git rev-parse --abbrev-ref HEAD >& /dev/null && echo "{`git rev-parse --abbrev-ref HEAD`}"'
set branch = `__git_current_branch`
set master = "%{\033[48;5;22m%}"
set d_color = "%{\033[48;5;88m%}"
set m_color = "%{\033[48;5;92m%}"
set u_color = "%{\033[48;5;202m%}"
set n_color = "%{\033[48;5;82m%}"
set a_color = "%{\033[48;5;130m%}"
set r_color = "%{\033[48;5;30m%}"
set clr = "%{\033[0m%}"

if ( "X${branch}" != "X" ) then
	# get current status of git repo
	set gstatus = `git status`
	set dirty = `echo -n "${gstatus}" | grep "modified:" >& /dev/null; echo "$?"`
	set untracked = `echo -n "${gstatus}" | grep "Untracked files" >& /dev/null; echo "$?"`
	set ahead = `echo -n "${gstatus}" | grep "Your branch is ahead of" >& /dev/null; echo "$?"`
	set newfile = `echo -n "${gstatus}" | grep "new file:" >& /dev/null; echo "$?"`
	set renamed = `echo -n "${gstatus}" | grep "renamed:" >& /dev/null; echo "$?"`
	set deleted = `echo -n "${gstatus}" | grep "deleted:" >& /dev/null; echo "$?"`
	set bits = ''
	set color = ''
	
	if ( "${renamed}" == "0" ) set bits = "${r_color} > ${clr}${bits}"
	if ( "${ahead}" == "0" ) set bits = "${a_color} * ${clr}${bits}"
	if ( "${newfile}" == "0" ) set bits = "${n_color} + ${clr}${bits}"
	if ( "${untracked}" == "0" ) set bits = "${u_color} ? ${clr}${bits}"
	if ( "${deleted}" == "0" ) set bits = "${d_color} x ${clr}${bits}"
	if ( "${dirty}" == "0" ) set bits = "${m_color} m ${clr}${bits}"
	#if ( "${bits}" != "" ) then
	  set rprompt = "${master}${branch}${bits}${clr} "
	#else
	#	set rprompt = "${master}${branch}${clr} "
	#endif
else
	set rprompt = ""
endif

